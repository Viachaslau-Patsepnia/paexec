#!/usr/bin/env runawk

# Copyright (c) 2009 Aleksey Cheusov <vle@gmx.net>
#
# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal in the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
# LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
# OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

#env "LC_ALL=C"

#use "alt_assert.awk"
#use "power_getopt.awk"

############################################################
#.begin-str help
# paexec_reorder - takes output of 'paexec -le' or 'paexec -gle' on
#     input and outputs ordered results, that is without "slicing".
# usage: paexec_reorder [OPTIONS]
# OPTIONS:
#    -h|--help                  display this help
#    -g                         Expects output of "paexec -gle",
#                               by default -- "paexec -le".
#    -S                         remove leading space
#    -l                         prepand output line with the task number
#                               given on input
#.end-str
############################################################

BEGIN {
	bad_input_msg = "bad input, did you run paexec -sle?"
	remove_spc  = getarg("S")
	graph_mode  = getarg("g")
	output_task = getarg("l")
}

function print_results (task,        cnt,i){
	if (!graph_mode || status [task] == "success" || status [task] == "failure"){
		cnt = count [task]
		for (i=1; i <= cnt; ++i){
			if (output_task)
				printf "%s ", task
			print line [task, i]
			delete line [task, i]
		}

		count [task] = 0
	}else if (status [task] == "fatal"){
		count [task] = 0
	}else{
		abort(bad_input_msg)
	}
}

{
	assert(NF > 0 && $0 ~ /^[0-9]+ /, bad_input_msg "1")
}

$0 ~ /^[0-9]+ $/ {
	print_results($1)
	next
}

{
	if (graph_mode)
		status [$1] = $2

	task = $1
	sub(/^[0-9]+ /, "", $0)
	cnt = ++count [task]

	if (remove_spc)
		sub(/^ /, "", $0)
	line [task, cnt] = $0

	if (task > task_max)
		task_max = task
}

END {
	# output of "paexec -l" (without -e) is also allowed, obviously
	# this requires more memory for reordering results, so applying
	# option -e is strongly recomended.
	for (i=1; i <= task_max; ++i){
		print_results(i)
	}
}
